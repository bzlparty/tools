load("@rules_go//go:def.bzl", "go_binary", "go_library")
load("//lib:defs.bzl", "GO_PLATFORMS", "sha")

go_library(
    name = "sha_lib",
    srcs = [
        "main.go",
    ],
    importpath = "github.com/bzlparty/tools/cmd/sha",
    visibility = ["//visibility:private"],
)

go_binary(
    name = "sha",
    embed = [":sha_lib"],
    visibility = ["//visibility:public"],
)

# Tagging all targets `manual` prevents `test //...` from building
# for all platforms.

[
    (
        go_binary(
            name = "sha_%s_%s" % (os, arch),
            embed = [":sha_lib"],
            goarch = arch,
            goos = os,
            tags = ["manual"],
            visibility = ["//visibility:public"],
        ),
        sha(
            name = "sha_%s_%s_sha384" % (os, arch),
            src = "sha_%s_%s" % (os, arch),
            tags = ["manual"],
            visibility = ["//visibility:public"],
        ),
    )
    for os, arch in GO_PLATFORMS
]

filegroup(
    name = "bins",
    srcs = ["sha_%s_%s" % (os, arch) for os, arch in GO_PLATFORMS],
    tags = ["manual"],
    visibility = ["//scripts/release:__pkg__"],
)

filegroup(
    name = "shasums",
    srcs = ["sha_%s_%s_sha384" % (os, arch) for os, arch in GO_PLATFORMS],
    tags = ["manual"],
    visibility = ["//scripts/release:__pkg__"],
)
