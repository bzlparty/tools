load("@rules_go//go:def.bzl", "go_binary", "go_library")
load("//lib:defs.bzl", "GO_PLATFORMS", "sha")

go_library(
    name = "templ_lib",
    srcs = [
        "assets.go",
        "main.go",
        "toolchains.go",
        "tools.go",
    ],
    importpath = "github.com/bzlparty/tools/cmd/templ",
    visibility = ["//visibility:private"],
)

go_binary(
    name = "templ",
    embed = [":templ_lib"],
    visibility = ["//visibility:public"],
)

# Tagging all targets `manual` prevents `test //...` from building
# for all platforms.

[
    (
        go_binary(
            name = "templ_%s_%s" % (os, arch),
            embed = [":templ_lib"],
            goarch = arch,
            goos = os,
            tags = ["manual"],
            visibility = ["//visibility:public"],
        ),
        sha(
            name = "templ_%s_%s_sha384" % (os, arch),
            src = "templ_%s_%s" % (os, arch),
            tags = ["manual"],
        ),
    )
    for os, arch in GO_PLATFORMS
]

filegroup(
    name = "bins",
    srcs = ["templ_%s_%s" % (os, arch) for os, arch in GO_PLATFORMS],
    tags = ["manual"],
    visibility = ["//scripts/release:__pkg__"],
)

filegroup(
    name = "shasums",
    srcs = ["templ_%s_%s_sha384" % (os, arch) for os, arch in GO_PLATFORMS],
    tags = ["manual"],
    visibility = ["//scripts/release:__pkg__"],
)
